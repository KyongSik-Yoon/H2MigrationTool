plugins {
    // Apply the application plugin to add support for building a CLI application in Java.
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version 'latest.release'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    implementation 'commons-io:commons-io:2.+'
    implementation 'org.apache.commons:commons-lang3:3.+'
    implementation 'commons-cli:commons-cli:1.+'
}

testing {
    suites {
        // Configure the built-in test suite
        test {
            // Use JUnit Jupiter test framework
            useJUnitJupiter('5.8.1')
        }
    }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

application {
    // Define the main class for the application.
    mainClass = 'com.manticore.h2.H2MigrationTool'
}

shadowJar {
    minimize()
    manifest {
        attributes 'Main-Class': 'com.manticore.h2.H2MigrationTool'
    }
}

task updateH2GitLog(type: Exec) {
    executable "sh"
    args "-c", "git --git-dir=../h2database/.git rev-list --topo-order -10000 HEAD --pretty=reference --abbrev-commit --reverse | sed -n '1p;0~2p' > src/main/resources/com/manticore/h2/h2-git.log"
}

task copyH2Jars(type: Exec) {
    executable "sh"
    args "-c", "cp ../h2database/h2/target/*.jar src/main/resources/drivers"
}

task changeJarFileExtensions(type: Exec) {
    executable "sh"
    args "-c", """
                for f in src/main/resources/drivers/*.jar; do 
                    mv -- \"\$f\" \"\${f%.jar}.bin\"
                done
               """
}

